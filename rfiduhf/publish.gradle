apply plugin: 'maven'
apply plugin: 'com.novoda.bintray-release'

def pomMetadata = [
        'groupId'   : project.group ?: rootProject.group,
        'artifactId': project.name,
        'version'   : project.version ?: rootProject.version,
        'desc'      : '深圳市朝攀科技有限公司CI30X RFID读卡器SDK',
        'website'   : 'https://github.com/chimpim/rfiduhf'
]

/**
 * maven plugin
 */
uploadArchives {
    repositories {
        mavenDeployer {
            pom.groupId = pomMetadata.groupId
            pom.artifactId = pomMetadata.artifactId
            pom.version = pomMetadata.version
            repository(url: "file:///" + rootProject.file(".repo").absolutePath)
        }
    }
}

task sourcesJar(type: Jar) {
    from sourceSets.main.java.srcDirs
    classifier = 'sources'
}

javadoc {
    options {
        encoding 'utf-8'
        charSet 'utf-8'
        author true
        version true
    }
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    from javadoc.destinationDir
    classifier = 'javadoc'
}

artifacts {
    archives sourcesJar
    archives javadocJar
}

/**
 * bintray-release plugin
 */
publish {
    bintrayUser = System.getenv('BINTRAY_USER')
    bintrayKey = System.getenv('BINTRAY_API_KEY')
    userOrg = 'tuuzed'
    repoName = 'maven'
    dryRun = false
    override = true
    groupId = pomMetadata.groupId
    artifactId = pomMetadata.artifactId
    publishVersion = pomMetadata.version
    desc = pomMetadata.desc
    website = pomMetadata.website
}